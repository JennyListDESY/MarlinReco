##############################################################################
# cmake configuration file for MarlinReco
#
# requires:
#   MacroCheckPackageLibs.cmake for checking package libraries
#
# returns following variables:
#
#   MarlinReco_FOUND      : set to TRUE if MarlinReco found
#       if FIND_PACKAGE called with REQUIRED and COMPONENTS arguments
#       MarlinReco_FOUND is only set to TRUE if ALL components are also found
#       if REQUIRED is NOT set components may or may not be available
#
#   MarlinReco_ROOT       : path to this MarlinReco installation
#   MarlinReco_VERSION    : package version
#   MarlinReco_LIBRARIES  : list of MarlinReco libraries (NOT including COMPONENTS)
#   MarlinReco_INCLUDE_DIRS  : list of paths to be used with INCLUDE_DIRECTORIES
#   MarlinReco_LIBRARY_DIRS  : list of paths to be used with LINK_DIRECTORIES
#   MarlinReco_COMPONENT_LIBRARIES      : list of MarlinReco component libraries
#   MarlinReco_${COMPONENT}_FOUND       : set to TRUE or FALSE for each library
#   MarlinReco_${COMPONENT}_LIBRARY     : path to individual libraries
#   MarlinReco_${COMPONENT}_LIB_DEPENDS : individual library dependencies
#
# @author Jan Engels, Desy
##############################################################################


SET( MarlinReco_ROOT "@CMAKE_INSTALL_PREFIX@" )
SET( MarlinReco_VERSION "@MarlinReco_VERSION@" )



# do not store find results in cache
SET( MarlinReco_INCLUDE_DIRS MarlinReco_INCLUDE_DIRS-NOTFOUND )
MARK_AS_ADVANCED( MarlinReco_INCLUDE_DIRS )

FIND_PATH( MarlinReco_INCLUDE_DIRS
    NAMES MarlinTrackFit.h
    PATHS "${MarlinReco_ROOT}/include"
    NO_DEFAULT_PATH
)



# ---------- libraries --------------------------------------------------------
INCLUDE( "@ILCSOFT_CMAKE_MODULES_ROOT@/MacroCheckPackageLibs.cmake" )

# only standard libraries should be passed as arguments to CHECK_PACKAGE_LIBS
# additional components are set by cmake in variable PKG_FIND_COMPONENTS
# first argument should be the package name
CHECK_PACKAGE_LIBS( MarlinReco MarlinReco )



# ---------- libraries dependencies -------------------------------------------
# this sets MarlinReco_${COMPONENT}_LIB_DEPENDS variables
INCLUDE( "${MarlinReco_ROOT}/lib/cmake/MarlinRecoLibDeps.cmake" )



# check if g2c/gfortran lib is available
# if somehow the library is in a different location you can tell cmake to
# also search in other locations for finding libraries.
# this can be done by setting a shell environment variable:
# $ export CMAKE_LIBRARY_PATH="/bla/bla:/foo/bar"
# or by calling:
# $ cmake -DCMAKE_LIBRARY_PATH="/bla/bla;/foo/bar" ...
# notice the path's separators! ( ':' for shell and ';' for cmake !! )
FIND_LIBRARY( FORTRAN_LIB NAMES @FORTRAN_LIBNAME@ PATHS @FORTRAN_LIB_PATHS@ ${MarlinReco_ROOT}/lib NO_DEFAULT_PATH )
# if previous find fails, try to find in system paths..
FIND_LIBRARY( FORTRAN_LIB NAMES @FORTRAN_LIBNAME@ )

IF( NOT FORTRAN_LIB )
    MESSAGE( STATUS "Check for MarlinReco: failed to find @FORTRAN_LIBNAME@ library!!" )
ELSE( NOT FORTRAN_LIB )
    MESSAGE( STATUS "Check for lib@FORTRAN_LIBNAME@: ${FORTRAN_LIB}" )
    MARK_AS_ADVANCED( FORTRAN_LIB )
ENDIF( NOT FORTRAN_LIB )




# ---------- final checking ---------------------------------------------------
INCLUDE( FindPackageHandleStandardArgs )
# set MarlinReco_FOUND to TRUE if all listed variables are TRUE and not empty
# MarlinReco_COMPONENT_VARIABLES will be set if FIND_PACKAGE is called with REQUIRED argument
FIND_PACKAGE_HANDLE_STANDARD_ARGS( MarlinReco DEFAULT_MSG MarlinReco_ROOT MarlinReco_INCLUDE_DIRS MarlinReco_LIBRARIES )

SET( MarlinReco_FOUND ${MARLINRECO_FOUND} )

